plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.ben-manes.versions'
    id 'org.cadixdev.licenser'
}

dependencyUpdates.rejectVersionIf { isNonStable(it.candidate.version) }
configurations.forEach{ it.transitive = false }

java.withSourcesJar()

dependencies {
    compileOnly("org.jetbrains:annotations:${JETBRAINS_ANNOTATIONS_VERSION}")

    api("org.ow2.asm:asm:${ASM_VERSION}")
    api("org.ow2.asm:asm-tree:${ASM_VERSION}")
    api("org.ow2.asm:asm-analysis:${ASM_VERSION}")
    api("org.ow2.asm:asm-commons:${ASM_VERSION}")
    api("net.minecraftforge:forgespi:${SPI_VERSION}")
    api("net.minecraftforge:mergetool:${MERGETOOL_VERSION}:api")
    api("org.apache.logging.log4j:log4j-api:${LOG4J_VERSION}")
    api("org.slf4j:slf4j-api:${SLF4J_API_VERSION}")
    api("com.google.guava:guava:${GUAVA_VERSION}")
    api("com.google.code.gson:gson:${GSON_VERSION}")
    api("org.apache.maven:maven-artifact:${APACHE_MAVEN_ARTIFACT_VERSION}")
    api("org.apache.commons:commons-lang3:${APACHE_COMMONS_LANG3_VERSION}")
    api("com.electronwill.night-config:core:${NIGHTCONFIG_VERSION}")
    api("com.electronwill.night-config:toml:${NIGHTCONFIG_VERSION}")
    api("cpw.mods:modlauncher:${MODLAUNCHER_VERSION}")
    api("net.minecraftforge:coremods:${COREMODS_VERSION}")
    api("com.mojang:logging:${MOJANG_LOGGING_VERSION}")
    api "net.minecraftforge:JarJarSelector:${JARJAR_VERSION}"
    api "net.minecraftforge:JarJarMetadata:${JARJAR_VERSION}"
    api("cpw.mods:securejarhandler:${SECUREJARHANDLER_VERSION}")

    implementation("net.sf.jopt-simple:jopt-simple:${JOPT_SIMPLE_VERSION}")
    implementation("net.minecraftforge:accesstransformers:${ACCESSTRANSFORMERS_VERSION}")
    implementation("net.minecrell:terminalconsoleappender:${TERMINALCONSOLEAPPENDER_VERSION}")
    // implementation("org.jline:jline:${JLINE_VERSION}")

    implementation("org.apache.logging.log4j:log4j-core:${LOG4J_VERSION}")
    annotationProcessor("org.apache.logging.log4j:log4j-core:${LOG4J_VERSION}")

    testCompileOnly("org.jetbrains:annotations:${JETBRAINS_ANNOTATIONS_VERSION}")
    testRuntimeOnly("cpw.mods:bootstraplauncher:${BOOTSTRAPLAUNCHER_VERSION}")
}

ext {
    MANIFESTS = [
        '/': [
            'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Git-Commit': GIT_INFO.abbreviatedId,
            'Git-Branch': GIT_INFO.branch,
            'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}",
        ] as LinkedHashMap,
        'net/minecraftforge/fml/loading/': [
            'Specification-Title':      'FMLLoader',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    '1',
            'Implementation-Title':     'FML Loader',
            'Implementation-Version':   '1.0',
            'Implementation-Vendor':    'Forge'
        ] as LinkedHashMap
    ]
}

jar.doFirst {
    MANIFESTS.each { pkg, values ->
        manifest.attributes(values, pkg)
    }
}

tasks.withType(JavaCompile) {
    doFirst {
        options.compilerArgs << '--module-path' << classpath.asPath
        classpath = files()
    }
    options.compilerArgs << '-Xlint:unchecked'
    options.javaModuleVersion = '1.0'
}

license {
    header = rootProject.file('LICENSE-header.txt')

    include 'net/minecraftforge/'
}

artifacts {
    archives jar
}

publishing.publications.mavenJava(MavenPublication) {
    from components.java
}